# Jedi Knight Cog Script
#
# CTF_DOORGOLD.COG
#
# CTF Gold Door cog script
# If 'delay' = -1.0 then door stays open
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
#
# Modified to support Rbots


symbols

thing       door                             mask=0x405 desc=door_object
flex        movespeed=3.0                    desc=movespeed_of_door
flex        delay=2.0                        desc=delay_before_close

int         doorsector=-1                    local
int         player                           local

int         msgbase=1000                     local

int         haskey                           local
int         keys                             local
cog         srv                              local
cog         srv2                             local

message     startup
message     touched
message     activated

end            

# ========================================================================================

code

startup:
   doorsector = GetThingSector(door);
   SectorAdjoins(doorsector, 0);

   Return;

# ........................................................................................

touched:
	if (GetThingType(GetSourceRef()) == 2)
	{
		// This is a bot, so treat the touch like an activate and open the door
		goto activated;
	}
	return;

# ........................................................................................

activated:
   if(IsMoving(door) == 1) Return;
   if(GetCurFrame(door) == 1) Return;
   
   player = GetSourceRef(); // Note: may be a player or a bot - check the thing type to find out

	haskey = 0;
	if (GetThingType(player) == 2)
	{
		// Check bot inventory for required key
		srv = GetThingCaptureCog(player);
		srv2 = SendMessageEx(srv, user7, 1100, -1, -1, -1);
		keys = SendMessageEx(srv2, user7, 1008, -1, -1, -1);
		if (BitTest(keys, 0x2)) haskey = 1;
	}
	else
	{
		// Check player inventory for required key
		if (GetInv(player, 84) == 1.0) haskey = 1;
	}
   
   // if the player carries the gold key
   // if((GetInv(player, 80) == 2.0) || (GetInv(player, 84) == 1.0))
   if (haskey)
   {
      SectorAdjoins(doorsector, 1);
      MoveToFrame(door, 1, movespeed);
      WaitForStop(door);
      if(delay == -1.0) Return;
      Sleep(delay);
      MoveToFrame(door, 0, movespeed);
      WaitForStop(door);
      SectorAdjoins(doorsector, 0);
   }
   else if (GetThingType(player) == 10) // Display key requirement message if this is a player
   {
      // Print("You need the Gold Key...");
      jkPrintUNIString(player, msgbase + 71);
   }
   
   Return;

end

