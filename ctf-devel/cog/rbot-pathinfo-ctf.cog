# Rbot Path Info Cog (CTF)
#
# Server-only
#
# This cog is used to enrich paths with CTF base navigation.
#
# Path ranks:
#
# When the bot is seeking a base and reaches an intersection between multiple
# paths, it will switch to the path with the highest priority for reaching that
# target base.  This ensures that the bot will reach the base as quickly and
# efficiently as possible.  The highest priority path is the one with the
# highest rank value.
#
# The rank value must be a non-negative integer (0,1,2,...) or -1 for no rank.
#
# Path run directions:
#
# In most cases, the bot will need to run in a certain direction on a path to
# reach the target base.  The valid run direction values are:
#   -1 = either direction
#    0 = backward only
#    1 = forward only
#
# Note that if the path is unidirectional, the run directions returned by this
# cog will be ignored, since the bot must always run forward on unidirectional
# paths.
#
# Rbots originally created by Raynar
# Additional code by Hell Raiser

flags=0x40

symbols

message	trigger

# Identifies the path that the info belongs to
int		pathid0=-1
int		pathid1=-1
int		pathid2=-1
int		pathid3=-1
int		pathid4=-1
int		pathid5=-1
int		pathid6=-1
int		pathid7=-1
int		pathid8=-1
int		pathid9=-1

# The rank for this path when seeking the red base
int		redrank0=-1
int		redrank1=-1
int		redrank2=-1
int		redrank3=-1
int		redrank4=-1
int		redrank5=-1
int		redrank6=-1
int		redrank7=-1
int		redrank8=-1
int		redrank9=-1

# The direction the bot must run on this path to reach the red base
int		redrundir0=-1
int		redrundir1=-1
int		redrundir2=-1
int		redrundir3=-1
int		redrundir4=-1
int		redrundir5=-1
int		redrundir6=-1
int		redrundir7=-1
int		redrundir8=-1
int		redrundir9=-1

# The rank for this path when seeking the gold base
int		goldrank0=-1
int		goldrank1=-1
int		goldrank2=-1
int		goldrank3=-1
int		goldrank4=-1
int		goldrank5=-1
int		goldrank6=-1
int		goldrank7=-1
int		goldrank8=-1
int		goldrank9=-1

# The direction the bot must run on this path to reach the gold base
int		goldrundir0=-1
int		goldrundir1=-1
int		goldrundir2=-1
int		goldrundir3=-1
int		goldrundir4=-1
int		goldrundir5=-1
int		goldrundir6=-1
int		goldrundir7=-1
int		goldrundir8=-1
int		goldrundir9=-1

# CTF game state
int		captured_red		local
int		captured_gold		local
thing	redflag_taken		local
thing	goldflag_taken		local

# Working variables
int		i					local
int		dest				local
thing	rbot				local
int		pathid				local
int		rank				local
int		rundir				local
int		redrank				local
int		redrundir			local
int		goldrank			local
int		goldrundir			local

end

code

trigger:
	// Server-only
	if (!IsServer()) return;

	if (GetSourceRef() == 12210) // CTF info update
	{
		captured_red = GetParam(0);
		captured_gold = GetParam(1);
		redflag_taken = GetParam(2);
		goldflag_taken = GetParam(3);
		return;
	}

	if (GetSourceRef() == 13100) // Path info request
	{
		pathid = GetParam(0);
		rbot = GetParam(1);

		// Look up the requested info
		redrank = -1;
		redrundir = -1;
		goldrank = -1;
		goldrundir = -1;
		for (i = 0 ; i < 10 ; i = i + 1)
		{
			if (pathid0[i] == pathid)
			{
				redrank = redrank0[i];
				redrundir = redrundir0[i];
				goldrank = goldrank0[i];
				goldrundir = goldrundir0[i];
			}
		}

		if (redrank == -1 && goldrank == -1) return; // No path info found

		// Figure out if the bot is trying to reach a base
		dest = -1; // Default - just wander
		if (GetThingUserData(rbot) == 1) // Bot is on the red team
		{
			if (captured_gold == 2 && rbot == goldflag_taken) // Bot has stolen the gold flag
			{
				if (captured_red == 0) // The red flag is at base
				{
					dest = 1; // Head for the red base to score
				}
			}
			else if (captured_gold == 0) // The gold flag is at base
			{
				dest = 2; // Head for the gold base to steal the flag
			}
		}
		else if (GetThingUserData(rbot) == 2) // Bot is on the gold team
		{
			if (captured_red == 2 && rbot == redflag_taken) // Bot has stolen the red flag
			{
				if (captured_gold == 0) // The gold flag is at base
				{
					dest = 2; // Head for the gold base to score
				}
			}
			else if (captured_red == 0) // The red flag is at base
			{
				dest = 1; // Head for the red base to steal the flag
			}
		}

		// Use the bot's destination to choose which path information to return
		rank = -1;
		rundir = -1;
		if (dest == 1) // Seeking the red base
		{
			rank = redrank;
			rundir = redrundir;
		}
		else if (dest == 2) // Seeking the gold base
		{
			rank = goldrank;
			rundir = goldrundir;
		}

		if (rank == -1) return; // There is no path info to return for this path right now

		// Send response
		SendTrigger(-1, 13110, pathid, rank, rundir, -1);
		return;
	}

	return;

end
