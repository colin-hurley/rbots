# Jedi Knight Cog Script
#
# C1_SECRETELEV.COG
#
# Script for C1 Secret Elevator
#
# This script handles trigger plates and a switch.
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved


symbols

thing       elevator                         desc=elev_thing
surface     switch                           desc=switch
flex        speed=8.0                        desc=speed
flex        delay=3.0                        desc=delay

surface     plate0                           desc=plate0
surface     plate1                           desc=plate1
surface     plate2                           desc=plate2
surface     plate3                           desc=plate3

int         dummy                            local

sound       on_snd=set_hi2.wav               local
sound       off_snd=lgclick1.wav             local

message     activated
message     entered
message     crossed
message     arrived
message     timer
message     blocked

end

# ========================================================================================

code

activated:
   if(GetSenderRef() != switch) Return;
   if(GetWallCel(switch) == 1) Return;
   if(IsMoving(elevator) == 1) Return;
   if(GetCurFrame(elevator) == 1) Return;

   SetWallCel(switch, 1);
   dummy = PlaySoundPos(on_snd, SurfaceCenter(switch), 1.0, 5.0, 10.0, 0);
   MoveToFrame(elevator, 1, speed);

   Return;

entered:
crossed:
   // forbids entering the elevator itself, as this causes
   // problems when a player walks on the non convex roof :
   // i.e. he enters, leaves, enters, leaves, ...
   // We'll replace this by crossing the adjoin just before the
   // lift down in the secret
   if(GetSenderRef() == elevator) Return;
   if(GetWallCel(switch) == 1) Return;
   if(IsMoving(elevator) == 1) Return;
   if(GetCurFrame(elevator) == 1) Return;

   SetWallCel(switch, 1);
   dummy = PlaySoundPos(on_snd, SurfaceCenter(switch), 1.0, 5.0, 10.0, 0);
   MoveToFrame(elevator, 1, speed);

   Return;

# ........................................................................................

arrived:
   if(GetCurFrame(elevator) == 1)
      SetTimerEx(delay, 1, 0, 0);
   else
   {
      SetWallCel(switch, 0);
      dummy = PlaySoundPos(off_snd, SurfaceCenter(switch), 1.0, 5.0, 10.0, 0);
   }
   Return;

# ........................................................................................

timer:
   MoveToFrame(elevator, 0, speed);
   Return;

# ........................................................................................

blocked:
   KillTimerEx(1);
   MoveToFrame(elevator, 1 - GetGoalFrame(elevator), speed);
   Return;

end


