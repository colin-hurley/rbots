# Jedi Knight Cog Script
#
# C2_MTRIGELEV.COG
#
# Script for C2 Multiple Triggers Elevators
#
# Handles up to 4 trigger plates and up to 4 crossable adjoins
# to trigger the lift movement to frame 1 at speed 'speed'.
# The lift will remain there for 'delay' seconds, then go back to frame 0.
# Note that entering the lift itself also triggers it.
#
# [YB]
#
# The script now handles two secret plates (or crossable adjoins) that
# trigger the lift and move it to frame 2. The 'speed' and the 'delay'
# variables are the same in both cases.
#
# [SD]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
#
# Modified to support Rbots


symbols

thing       elevator                         mask=0x405 desc=elev_thing
flex        speed=8.0                        desc=speed
flex        delay=3.0                        desc=delay

surface     plate0                           mask=0x405 desc=plate0
surface     plate1                           mask=0x405 desc=plate1
surface     plate2                           mask=0x405 desc=plate2
surface     plate3                           mask=0x405 desc=plate3
surface     secret1                          mask=0x405 desc=secret1
surface     secret2                          mask=0x405 desc=secret2

surface     cross0                           mask=0x405 desc=cross0
surface     cross1                           mask=0x405 desc=cross1
surface     cross2                           mask=0x405 desc=cross2
surface     cross3                           mask=0x405 desc=cross3

int         pending=0                        local

message     entered
message     crossed
message     arrived
message     timer
message     blocked

end

# ========================================================================================

code

//entered:
//crossed:  
// if(IsMoving(elevator) == 1) Stop;
// if(GetCurFrame(elevator) == 1) Stop;
// if(GetCurFrame(elevator) == 2) Stop;
//
// if((GetSenderRef() != secret1) && (GetSenderRef() != secret2))
// {
//    MoveToFrame(elevator, 1, speed);
//    WaitForStop(elevator);
//    Sleep(delay);
//    MoveToFrame(elevator, 0, speed);
//    WaitForStop(elevator);
// }
// else
// {
//    MoveToFrame(elevator, 2, speed);
//    WaitForStop(elevator);
//    Sleep(delay);
//    MoveToFrame(elevator, 0, speed);
//    WaitForStop(elevator);
// }
// Return;


entered:
crossed:
   if(IsMoving(elevator) == 1) Return;
   if(GetCurFrame(elevator) == 1) Return;
   if(GetCurFrame(elevator) == 2) Return;

   // The 'pending' symbol will be 1 if a thread is blocked on the Sleep below.
   // Since we are about to start moving anyway, just ignore any excess messages.
   if (pending) return;

   if (GetThingType(GetSourceRef()) == 2) // bot
   {
      // Before moving the elevator, give the bot a short moment to reach the node
      pending = 1;
      Sleep(0.25);
      pending = 0;
   }

   if((GetSenderRef() != secret1) && (GetSenderRef() != secret2))
      MoveToFrame(elevator, 1, speed);
   else
      MoveToFrame(elevator, 2, speed);

   Return;

# ........................................................................................

arrived:
   if((GetCurFrame(elevator) == 1) || (GetCurFrame(elevator) == 2))
      SetTimerEx(delay, 1, 0, 0);

   Return;

# ........................................................................................

timer:
   MoveToFrame(elevator, 0, speed);
   Return;

# ........................................................................................

blocked:
   KillTimerEx(1);
   MoveToFrame(elevator, 1 - GetGoalFrame(elevator), speed);
   Return;

end

