# Jedi Knight Cog Script
#
# C3_MTRIGELEV.COG
#
# Script for C3 Multiple Triggers Elevators.
#
# Handles up to 4 trigger plates and up to 4 crossable adjoins
# to trigger the lift movement to frame 1 at speed 'speed'.
# The lift will remain there for 'delay' seconds, then go back to frame 0.
# Note that entering the lift itself also triggers it.
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
#
# Modified to support Rbots


symbols

thing       elevator                         mask=0x405 desc=elev_thing
flex        speed=8.0                        desc=speed
flex        delay=3.0                        desc=delay

surface     plate0                           mask=0x405 desc=plate0
surface     plate1                           mask=0x405 desc=plate1
surface     plate2                           mask=0x405 desc=plate2
surface     plate3                           mask=0x405 desc=plate3

surface     cross0                           mask=0x405 desc=cross0
surface     cross1                           mask=0x405 desc=cross1
surface     cross2                           mask=0x405 desc=cross2
surface     cross3                           mask=0x405 desc=cross3

int         pending=0                        local

message     entered
message     crossed
message     arrived
message     timer
message     blocked

end

# ========================================================================================

code

entered:
crossed:
   if(IsMoving(elevator) == 1) Return;
   if(GetCurFrame(elevator) == 1) Return;

   // The 'pending' symbol will be 1 if a thread is blocked on the Sleep below.
   // Since we are about to start moving anyway, just ignore any excess messages.
   if (pending) return;

   if (GetThingType(GetSourceRef()) == 2) // bot
   {
      // Before moving the elevator, give the bot a short moment to reach the node
      pending = 1;
      Sleep(0.25);
      pending = 0;
   }

   MoveToFrame(elevator, 1, speed);

   Return;

# ........................................................................................

arrived:
   if(GetCurFrame(elevator) == 1)
      SetTimerEx(delay, 1, 0, 0);
   Return;

# ........................................................................................

timer:
   MoveToFrame(elevator, 0, speed);
   Return;

# ........................................................................................

blocked:
   KillTimerEx(1);
   MoveToFrame(elevator, 1 - GetGoalFrame(elevator), speed);
   Return;

end

