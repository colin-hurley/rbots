# Rbot Administration - Highlight
#
# This utility will "highlight" things of interest for a bot (e.g. destination
# node, enemy target, item).
#
# See also: rbot-admin.cog
#
# Rbots originally created by Raynar

flags=0x240

symbols

message startup
message shutdown
message user0
message user1
message trigger
message pulse

int active=0 local

# We can't dynamically create 'ghost' things (they can only be statically
# placed in the JKL), so we create '_ghostdecor' things instead, which are
# virtually identical.
template	ghost=_ghostdecor	local

cog srv=-1 local
int botmode local
thing target local
thing enemy local
thing item local
thing nodeghost=-1 local
flex nodeposx local
flex nodeposy local
flex nodeposz local
vector nodepos local
sector nodesec local

end

code

startup:
	// Register functions
	SendMessageEx(GetInvCog(GetLocalPlayerThing(), 118), user0, -1, -1, -1, -1);
	return;

shutdown:
	// Reinitialize to default values when exiting level
	active = 0;
	srv = -1;
	nodeghost = -1;
	return;

// Execute
user0:
	active = 1 - active;
	if (active) {
		// Activate highlighter
		Print("Highlighter on");
		SetPulse(0.1);
	} else {
		Print("Highlighter off");
		SetPulse(0);
		jkEndTarget();
		if (nodeghost != -1) {
			DestroyThing(nodeghost);
			nodeghost = -1;
		}
	}
	return;

// Concat current state
user1:
	if (active) jkStringConcatAsciiString("Highlight target (ACTIVE)");
	else jkStringConcatAsciiString("Highlight target (NOT ACTIVE)");
	return;

trigger:
	if (GetSourceRef() == 15080) {
		// New bot selected
		srv = GetParam(1);
	}
	return;

pulse:
	target = -1;
	if (srv != -1) {
		botmode = SendMessageEx(srv, user7, 1003, -1, -1, -1);
		if (BitTest(botmode, 0x10000)) {
			// Bot is alive
			enemy = SendMessageEx(srv, user7, 1010, -1, -1, -1);
			item = SendMessageEx(srv, user7, 1011, -1, -1, -1);
			nodeposx = SendMessageEx(srv, user7, 1012, 1, -1, -1);
			nodeposy = SendMessageEx(srv, user7, 1012, 2, -1, -1);
			nodeposz = SendMessageEx(srv, user7, 1012, 3, -1, -1);
			nodepos = VectorSet(nodeposx, nodeposy, nodeposz);
			nodesec = SendMessageEx(srv, user7, 1012, 4, -1, -1);
			if (BitTest(botmode, 0x4000)) {
				// Bot has enemy target
				jkSetTargetColors(6, 7, 8); // Red
				target = enemy;
			} else if (item != -1) {
				// Bot is heading for an item
				jkSetTargetColors(1, 2, 3); // Green
				target = item;
			} else {
				// Otherwise, highlight the bot's destination node
				jkSetTargetColors(16, 17, 18); // Light Blue

				// Create a temporary ghost thing that we can use to "highlight" the node position
				if (nodeghost != -1) DestroyThing(nodeghost); // Remove previous ghost
				nodeghost = CreateThingAtPos(ghost, nodesec, nodepos, '0 0 0'); // Create new ghost with correct position and sector
				target = nodeghost;
			}
		}
	}
	if (target != -1) {
		jkSetTarget(target);
	} else {
		jkEndTarget();
	}
	return;

end
