# Rbot Skin Cog
#
# Server-only
# Type ID: 1030
#
# Rbots originally created by Raynar
# Additional code by Hell Raiser

flags=0x80

symbols

message		startup
message		user7

int			skinid=0

# The template to create the bot
template	rtemp=rbot_kylered

# Weapon mesh models
model		weapmesh0=red-0.3do			
model		weapmesh1=red-1.3do			
model		weapmesh2=red-2.3do
model		weapmesh3=red-3.3do
model		weapmesh4=red-4.3do
model		weapmesh5=red-5.3do
model		weapmesh6=red-6.3do
model		weapmesh7=red-7.3do
model		weapmesh8=red-8.3do
model		weapmesh9=red-9.3do
model		weapmesh10=red-10.3do

# Transient working vars
thing		rbot								local
thing		spawnpoint							local
int			curweap								local
int			geo									local

end

code

startup:
	if (IsMulti() && !IsServer()) return; // Host only

	// Allow other server-side cogs to discover this one by its type
	SendTrigger(-1, 1852000, 1030, GetSelfCog(), skinid, -1);

	return;

// Execute public API function for this cog
// Param 0 = Function ID
// The remaining params will be processed according to the requested function
// For example, param1 might be an index into an array.
// Returns the function-specific result of the execution
user7:
	if (GetParam(0) == 10303000) // create bot
	{
		spawnpoint = GetParam(1);
		rbot = CreateThing(rtemp, spawnpoint);
		ReturnEx(rbot);
		return;
	}

	if (GetParam(0) == 10303010) // change weapon model
	{
		rbot = GetParam(1);
		curweap = GetParam(2);
		// Setting a thing's model resets its current geo mode
		// Preserve current geo mode to avoid bot becoming visible with force persuasion active
		geo = GetThingCurGeoMode(rbot);
		SetThingModel(rbot, weapmesh0[curweap]);
		SetThingCurGeoMode(rbot, geo);
		ReturnEx(0);
		return;
	}

	// Report error if function ID does not match anything above
	jkStringClear();
	jkStringConcatFormattedInt(GetSenderRef(), "rbot-skin.cog :: ERROR :: Cog %d");
	jkStringConcatFormattedInt(GetParam(0), " has requested an invalid function ID, %d");
	jkStringOutput(-1, -1);
	return;

end
