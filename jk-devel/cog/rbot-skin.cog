# Rbot Skin Cog
#
# Server-only
#
# Rbots originally created by Raynar
# Additional code by Hell Raiser

flags=0x80

symbols

message		trigger

int			skinid=0

# The template to create the bot
template	rtemp=rbot_kylered

# Weapon mesh models
model		weapmesh0=red-0.3do			
model		weapmesh1=red-1.3do			
model		weapmesh2=red-2.3do
model		weapmesh3=red-3.3do
model		weapmesh4=red-4.3do
model		weapmesh5=red-5.3do
model		weapmesh6=red-6.3do
model		weapmesh7=red-7.3do
model		weapmesh8=red-8.3do
model		weapmesh9=red-9.3do
model		weapmesh10=red-10.3do

# Transient working vars
int			botid								local
thing		rbot								local
thing		spawnpoint							local
int			curweap								local

end

code

trigger:
	if (IsMulti() && !IsServer()) return; // Host only

	// Ignore requests that are not for this skin instance
	if (GetParam(0) != skinid) return;

	if (GetSourceRef() == 101060) // create bot
	{
		botid = GetParam(1);
		spawnpoint = GetParam(2);
		rbot = CreateThing(rtemp, spawnpoint);
		// Return created bot thing number
		SendTrigger(-1, 101061, botid, rbot, -1, -1);
		return;
	}

	if (GetSourceRef() == 101062) // change weapon model
	{
		rbot = GetParam(1);
		curweap = GetParam(2);
		SetThingModel(rbot, weapmesh0[curweap]);
		return;
	}

	return;


end


