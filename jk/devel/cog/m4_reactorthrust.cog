# Jedi Knight Cog Script
#
# M4_reactorthrust.cog
#
# Multiplayer sectorthrust trap.
#
# This script will control 2 sector thrust sectors thrusting in the same direction
# and with the same velocity.  The thrusts activate with a switch.
#
# [IS/JS]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved		
#
# Modified to support Rbots
# ========================================================================================

symbols

message		startup	
message		activate	
message		arrived
message		entered
message		timer	

int			currentThing		local
int			i						local

vector		playerVec			local
vector		vec0					desc=thrust_direction

float			thrustspeed=1.0	desc=thrust_speed
float			doorspeed=2.0 		desc=speed_of_the_doors
float			sleeptime=2.0		desc=seconds_that_the_doors_pause

thing			door0					linkid=2
thing			door1					nolink
thing			door2					nolink
thing			door3					nolink

surface		Switch				linkid=1

sector		sector0				mask=0x405
sector		sector1				mask=0x405

thing			light0				nolink
float			lspeed=4.0			desc=light_speed
sound			wav0
sound			wav1
thing			thing0
float			fadetime=4.0

int			channel=-1			local

# Rbots support

message		trigger

int			botid								local
thing		rbot								local
surface		buttonref							local
flex		timeout=10.0						local
int			waiting=0							local

end

# ========================================================================================

code
startup:
	SetTimer(0.1);
	channel=PlaySoundThing(wav0, thing0, 0.01, -1, -1, 1);
	return;
	
# ........................................................................................

activate:
	if (GetSenderID() != 1) return;
	rbot = -1;

do_activate:
	if (GetWallCel(switch) == 1) return; 

	SetWallCel(Switch, 1);
	PlaySoundPos(wav1, SurfaceCenter(switch), 1, -1, -1, 0);
	ChangeSoundVol(channel, 1.5, fadetime/2); 
	ChangeSoundPitch(channel, 1, fadetime/2); 
	MoveToFrame(door0, 1, doorspeed);
	MoveToFrame(door1, 1, doorspeed);
	MoveToFrame(door2, 1, doorspeed);
	MoveToFrame(door3, 1, doorspeed);
	SectorThrust(sector0, vec0, thrustspeed);
	SectorThrust(sector1, vec0, thrustspeed);
	MoveToFrame(light0, 1, lspeed);

	// unattach all players in sectors
	currentThing = FirstThingInSector(sector0);
	while (currentThing != -1) {
		call reactorpull;
		currentThing = NextThingInSector(currentThing);
	}
	
	currentThing = FirstThingInSector(sector1);
	while (currentThing != -1) {
		call reactorpull;
		currentThing = NextThingInSector(currentThing);
	}	

	if (rbot != -1)
	{
		// Set the waiting flag so we know to send the arrival event when the trap
		// deactivates
		waiting = 1;

		// Tell bot to wait
		botid = SendMessageEx(GetThingClassCog(rbot), user7, 10201000, -1, -1, -1);
		SendTrigger(-1, 11040, botid, GetSelfCog(), 1, timeout);
	}

	return;

# ........................................................................................

arrived:  
	if (GetCurFrame(GetSenderRef()) == 0) {
		SetWallCel(switch, 0);
		PlaySoundPos(wav1, SurfaceCenter(switch), 1, -1, -1, 0);
		SectorThrust(sector0, vec0, 0.0);
		SectorThrust(sector1, vec0, 0.0);

		if (waiting) // there is a bot waiting for this event
		{
			// Clear the waiting flag - no need to send another arrival event
			// until next time a bot is waiting for one
			waiting = 0;

			// Broadcast arrival event to all bots
			SendTrigger(-1, 11050, GetSelfCog(), 1, -1, -1);
		}
	} else {
		MoveToFrame(door0, 0, doorspeed);
		MoveToFrame(door1, 0, doorspeed);
		MoveToFrame(door2, 0, doorspeed);
		MoveToFrame(door3, 0, doorspeed);
		MoveToFrame(light0, 0, lspeed);
		ChangeSoundVol(channel, 0.1, fadetime); 
		ChangeSoundPitch(channel, 0.1, fadetime); 
	}
	
	return;

# ........................................................................................

timer:
	ChangeSoundPitch(channel, 0.1, 0.1); 
	return;
		
# ........................................................................................

entered:
	if (GetSenderId() !=0) return;
	if (GetWallCel(switch) !=1) return;
	currentThing = GetSourceRef();
	// fall through
	
# ........................................................................................

reactorpull:
	if (GetThingType(currentThing) == 2 || GetThingType(currentThing) == 10) {
		DetachThing(currentThing);
		playerVec = GetThingVel(currentThing);
		playerVec = VectorAdd(vec0, playerVec);
		SetThingVel(currentThing, playerVec);
	}
	return;

trigger:
	if (IsMulti() && !IsServer()) return; // Host only

	if (GetSourceRef() == 11031) // "activate" trigger
	{
		rbot = GetParam(0);
		buttonref = GetParam(1);
		if (switch == buttonref) goto do_activate;
		return;
	}
	return;

end
 