# Rbot Jump Cog
#
# Server-only
#
# This cog makes a bot jump upon reaching a path node.
#
# Rbots originally created by Raynar

flags=0x240

symbols

message		trigger
message		timer

# Activate the effect when the bot reaches this node
thing		node=-1								nolink

# Activate the effect when the bot is running in this direction
# -1 = forward or backward
# 0 = backward only
# 1 = forward only
int			rundir=-1

# Activation of the effect will be delayed by this duration
# A delay of 0 will activate the effect immediately
flex		delay=0.0

# The jump force will be scaled by this value
flex		scale=1.0

# Transient working vars
thing		rbot								local
thing		rbotnode							local
int			rbotrundir							local

end

code

trigger:
	if (IsMulti() && !IsServer()) return; // Host only

	if (GetSourceRef() == 11010) // Bot arrived at node
	{
		#botid = GetParam(0); // which bot requested the info
		rbot = GetParam(1); // the bot thing
		rbotnode = GetParam(2); // which node the bot arrived at
		rbotrundir = GetParam(3); // which direction the bot is running

		// Bot must be at the node
		if (rbotnode != node) return;

		// Bot must be running the right direction
		if (rbotrundir != rundir && rundir != -1) return;

		// Check for delay
		if (delay > 0)
		{
			SetTimerEx(delay, 1, rbot, GetThingSignature(rbot));
			return;
		}

		goto tryjump;
		return;
	}

	return;

timer:
	if (GetSenderId() == 1)
	{
		rbot = GetParam(0);

		// Make sure the bot thing reference is still valid
		if (GetThingSignature(rbot) != GetParam(1)) return;

		goto tryjump;
		return;
	}
	return;

tryjump:
	// Make sure the bot is still alive
	if (GetThingHealth(rbot) <= 0) return;

	// The bot can jump if he is on the ground or at the water surface
	if (GetAttachFlags(rbot) || GetPhysicsFlags(rbot) & 0x100000)
	{
		DetachThing(rbot);
		ApplyForce(rbot, VectorScale(VectorAdd(GetThingLVec(rbot), '0 0 90'), 2.5 * scale));
		return;
	}

	// Bot is not able to jump now, try again after a short delay
	SetTimerEx(0.1, 1, rbot, GetThingSignature(rbot));
	return;

end
