# Rbot Navigation Special Cog
#
# Server-only
# Type ID: 1061
#
# This cog supplies special navigation information for path nodes.
#
# Rbots originally created by Raynar

flags=0x280

symbols

message		startup
message		trigger
message		user7

# Special nodes
thing		navsp0=-1							nolink
thing		navsp1=-1							nolink
thing		navsp2=-1							nolink
thing		navsp3=-1							nolink
thing		navsp4=-1							nolink
thing		navsp5=-1							nolink
thing		navsp6=-1							nolink
thing		navsp7=-1							nolink
thing		navsp8=-1							nolink
thing		navsp9=-1							nolink
thing		navsp10=-1							nolink
thing		navsp11=-1							nolink
thing		navsp12=-1							nolink
thing		navsp13=-1							nolink
thing		navsp14=-1							nolink
thing		navsp15=-1							nolink
thing		navsp16=-1							nolink
thing		navsp17=-1							nolink
thing		navsp18=-1							nolink
thing		navsp19=-1							nolink

# Flags for each special node
int			navspinf0=0
int			navspinf1=0
int			navspinf2=0
int			navspinf3=0
int			navspinf4=0
int			navspinf5=0
int			navspinf6=0
int			navspinf7=0
int			navspinf8=0
int			navspinf9=0
int			navspinf10=0
int			navspinf11=0
int			navspinf12=0
int			navspinf13=0
int			navspinf14=0
int			navspinf15=0
int			navspinf16=0
int			navspinf17=0
int			navspinf18=0
int			navspinf19=0

# The first cog in the linked list
cog			spnav=-1							local
# The next cog following this one in the linked list
cog			nextspnav=-1						local

# Working variables
int			i									local
thing		node								local

end

# ========================================================================================

code

startup:
	if (IsMulti() && !IsServer()) return; // Host only

	// Allow other server-side cogs to discover this one by its type
	// Only send the discovery trigger for the head of the list
	// Any other cogs can append themselves to the list afterward
	if (spnav == -1) SendTrigger(-1, 1852000, 1061, GetSelfCog(), -1, -1); // register as head of list
	else SendMessageEx(spnav, user7, 10610001, GetSelfCog(), -1, -1); // append to list
	return;

trigger:
	if (IsMulti() && !IsServer()) return; // Host only

	if (GetSourceRef() == 1852000) // Rbot cog type discovery
	{
		if (GetParam(0) == 1061) // rbot-nav-special.cog
		{
			spnav = GetParam(1);
		}
		return;
	}

	return;

user7:
	if (GetParam(0) == 10610001) // Append cog to list
	{
		if (nextspnav == -1) nextspnav = GetParam(1); // this is the end of the list, append here
		else SendMessageEx(nextspnav, user7, GetParam(0), GetParam(1), -1, -1); // try to append to next cog
		return;
	}

	if (GetParam(0) == 10613000) // Get nav special mode
	{
		node = GetParam(1);

		// Search current cog for special info
		for (i = 0 ; i < 20 ; i = i + 1)
		{
			if (navsp0[i] == node && navspinf0[i]) // This is the requested node and it has special info
			{
				ReturnEx(navspinf0[i]);
				return;
			}
		}

		// No special info specified in this cog, see if we can delegate to the next cog in the list
		if (nextspnav != -1)
		{
			// Recursively delegate to next cog in the list
			ReturnEx(SendMessageEx(nextspnav, user7, GetParam(0), GetParam(1), GetParam(2), GetParam(3)));
			return;
		}

		// No more cogs in the list, no special info
		ReturnEx(0x0);
		return;
	}

	// Report error if function ID does not match anything above
	jkStringClear();
	jkStringConcatFormattedInt(GetSenderRef(), "rbot-nav-special.cog :: ERROR :: Cog %d");
	jkStringConcatFormattedInt(GetParam(0), " has requested an invalid function ID, %d");
	jkStringOutput(-1, -1);
	return;

end
