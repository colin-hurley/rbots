# Rbot Door Cog
#
# Door Script
#
# This is a customized version of 00_doubledoor.cog by LucasArts that has been updated
# to support Rbots. It can be used as a drop-in replacement for any case where the
# original is used.
#
# Rbots originally created by Raynar

symbols
message		startup		
message		activate	
message		arrived		
message		blocked		
message		entered
message		touched

thing		door0		linkid=0 mask=0x405
thing		door1		linkid=1 mask=0x405

flex		movespeed=8.0
flex		sleeptime=2.0
flex		lightvalue=0.5
sector		doorsector	local
int		numdoors	local
int		doorstatus	local
int		movestatus	local
sector		doorsector2	local
end

## Code Section

code

# .................................................................................................

startup:
	if (door0 >= 0)	numdoors = numdoors + 1;
	if (door1 >= 0) numdoors = numdoors + 1;

	doorsector = getthingsector(door0);
	doorsector2 = getthingsector(door1);

	sectoradjoins(doorsector, 0);
	sectoradjoins(doorsector2, 0);

	sectorlight(doorsector, lightvalue, 0.0);      		// add some light to door sector
	sectorlight(doorsector2, lightvalue, 0.0);
	return;

# .................................................................................................

activate:
	call checkstatus;
	if (movestatus) return;
	if (doorstatus == 0) {					// all pieces are at frame 0
		sectoradjoins(doorsector, 1);
		sectoradjoins(doorsector2, 1);
		call open_doors;
	}
	return;

# .................................................................................................

arrived:
	call checkstatus;
	if (movestatus) return;
	if (doorstatus == numdoors) {				// all pieces are at frame 1
		sleep(sleeptime);
		call close_doors;
	} else if (doorstatus == 0) {				// all pieces are at frame 0
		sectoradjoins(doorsector, 0);
		sectoradjoins(doorsector2, 0);
	}
	return;

# .................................................................................................

blocked:
	call open_doors;
	return;

# .................................................................................................

open_doors:
	movetoframe(door0, 1, movespeed);
	if (door1 >= 0) movetoframe(door1, 1, movespeed);
	return;

# .................................................................................................

close_doors:
	movetoframe(door0, 0, movespeed);
	if (door1 >= 0) movetoframe(door1, 0, movespeed);
	return;

# .................................................................................................

checkstatus:
	movestatus = ismoving(door0);
	if (door1 >= 0) movestatus = movestatus + ismoving(door1);

	doorstatus = getcurframe(door0);
	if (door1 >= 0) doorstatus = doorstatus + getcurframe(door1);
	return;

# ........................................................................................

entered:
	if (GetThingType(GetSourceRef()) != 2) return; // bots only
	goto activate;
	return;

# ........................................................................................

touched:
	if (GetThingType(GetSourceRef()) != 2) return; // bots only
	goto activate;
	return;

end

