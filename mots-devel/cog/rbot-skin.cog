# Rbot Skin Cog (MotS variant)
#
# Server only - synch
#
# Rbots originally created by Raynar
# Additional code by Hell Raiser

flags=0x80

symbols

message		trigger

int			skinid=0

# The template to create the bot
template	rtemp=rbot_kylered

# Weapon mesh models
model		weapmesh0=sabg.3do
model		weapmesh1=fistg.3do
model		weapmesh2=bryg.3do
model		weapmesh3=strg.3do
model		weapmesh4=detg.3do
model		weapmesh5=bowg.3do
model		weapmesh6=rptg.3do
model		weapmesh7=rldg.3do
model		weapmesh8=seqg.3do
model		weapmesh9=cong.3do
model		weapmesh10=sabgred.3do

# Transient working vars
int			botid			local
thing		rbot			local
thing		spawnpoint		local
int			curweap			local

end

code

trigger:
	if (IsMulti() && !IsServer())
	{
		// Server-only
		return;
	}

	// Ignore requests that are not for this skin instance
	if (GetParam(0) != skinid) return;

	if (GetSourceRef() == 101060) // create bot
	{
		botid = GetParam(1);
		spawnpoint = GetParam(2);
		rbot = CreateThing(rtemp, spawnpoint);
		// Return created bot thing number
		SendTrigger(-1, 101061, botid, rbot, -1, -1);
		return;
	}

	if (GetSourceRef() == 101062) // change weapon model
	{
		rbot = GetParam(1);
		curweap = GetParam(2);
		jkSetWeaponMesh(rbot, weapmesh0[curweap]);
		return;
	}

	return;


end


