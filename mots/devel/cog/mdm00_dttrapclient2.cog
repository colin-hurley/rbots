# Jedi Knight Cog Script
#
# MDM00_DTTRAPCLIENT.cog
#
# This will generate a series of 4 TD's per 'round' when the switch is activated.
# The trap resets in 'delay' seconds afterwards. Client side.
#
# [YB]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved
#
# Modified to support Rbots

flags=0x240

symbols

surface  switch                              desc=switch

thing    tdg1                                nolink,desc=tdg1
thing    tdg2                                nolink,desc=tdg2
thing    tdg3                                nolink,desc=tdg3
thing    tdg4                                nolink,desc=tdg4

int      rounds=2                            desc=rounds

flex     rate=0.1                            desc=rate
flex     delay=3.0                           desc=delay

int      cur_round=0                         local
int      dummy                               local
int      parent                              local

template proj_tpl=+grenade2                  local

sound    on_snd=set_hi2.wav                  local
sound    off_snd=lgclick1.wav                local

message  trigger

end

# ========================================================================================

code

trigger:

   // Handle only TRIGGER_DTTRAP
   if(GetSourceRef() != 11002) Return;

   // Save parent value now so the value is not lost if another trigger message
   // runs before we finish executing the code below. Subsequent trigger
   // messages may run during any of the sleeps. If this happens, the param
   // values will be replaced with those of the latest message.
   parent = GetParam(0);

   dummy = SetWallCel(switch, 1);
   dummy = PlaySoundPos(on_snd, SurfaceCenter(switch), 1.0, 5.0, 10.0, 0);

   cur_round = 0;
   while(cur_round < rounds)
   {
      dummy = CreateThing(proj_tpl, tdg1);

      // give it the correct parent via the parent's guid passed in the trigger this
      // is essential to credit the kill to the guy who activated the switch..
      SetThingParent(dummy, parent);
      Sleep(rate);
      
      dummy = CreateThing(proj_tpl, tdg2);
      SetThingParent(dummy, parent);
      Sleep(rate);
      
      dummy = CreateThing(proj_tpl, tdg3);
      SetThingParent(dummy, parent);
      Sleep(rate);
      
      dummy = CreateThing(proj_tpl, tdg4);
      SetThingParent(dummy, parent);
      Sleep(rate);
      
      cur_round = cur_round + 1;
   }

   Sleep(delay);

   dummy = SetWallCel(switch, 0);
   dummy = PlaySoundPos(off_snd, SurfaceCenter(switch), 1.0, 5.0, 10.0, 0);

   Return;

end


