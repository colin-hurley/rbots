#  Jedi Knight Missions Cog Script
#
#  MDM02_CARBOSERVER.COG
#
#  Carbonite freeze everything in a sector upon activation.
#
#  [YB]
#
#  1997 (C) LucasArts Entertainment Co.  All Rights Reserved.
#
# Modified to support Rbots

symbols

thing    console
thing	   platform
flex     freezefreq=5.0

int      canfreezenow=1                      local

message  activated
message  startup
message  timer
message  user0

sound    wav0=09pipesdetatch.wav             local
sound    wav1=09pipesmove.wav                local

# Rbots support

# Transient working variables
#thing       rbot                             local
thing       consoleref                       local

message     trigger

end

# ========================================================================================

code

startup:
   SetMasterCOG(GetSelfCOG());
   Return;

# ........................................................................................

activated:
   if ((GetSenderRef()==platform)&&(canfreezenow))
	{
	canfreezenow=0;
      PlaySoundThing(wav0, platform, 1.5, -1, -1, 0);
      PlaySoundThing(wav1, platform, 1.5, -1, -1, 0);
	MoveToFrame(platform,1,2);
	WaitForStop(platform);
	Sleep(1.0);
      PlaySoundThing(wav0, platform, 1.5, -1, -1, 0);
      PlaySoundThing(wav1, platform, 1.5, -1, -1, 0);
	MoveToFrame(platform,0,2);
	WaitForStop(platform);
      SetTimerEx(1.0, 1, 0, 0);
	}
   if((canfreezenow)&&(GetSenderRef()==console))
   {
      // Send a TRIGGER_CARBOFREEZE with its parameters
      SendTrigger(-1, 10001, 0, 0, 0, 0);
      canfreezenow = 0;
      SetTimerEx(freezefreq, 1, 0, 0);
   }
   Return;
user0:
   if(canfreezenow)
   {
      // Send a TRIGGER_CARBOFREEZE with its parameters
      SendTrigger(-1, 10001, 0, 0, 0, 0);
      canfreezenow = 0;
      SetTimerEx(freezefreq, 1, 0, 0);
   }
   Return;

# ........................................................................................

timer:
   canfreezenow = 1;
   Return;

# ........................................................................................

trigger:
	if (IsMulti() && !IsServer()) return; // Host only

	if (GetSourceRef() == 11032) // "activate" trigger
	{
		#rbot = GetParam(0);
		consoleref = GetParam(1);

		if (platform != -1 && platform == consoleref)
		{
			if (canfreezenow)
			{
				canfreezenow = 0;
				PlaySoundThing(wav0, platform, 1.5, -1, -1, 0);
				PlaySoundThing(wav1, platform, 1.5, -1, -1, 0);
				MoveToFrame(platform,1,2);
				WaitForStop(platform);
				Sleep(1.0);
				PlaySoundThing(wav0, platform, 1.5, -1, -1, 0);
				PlaySoundThing(wav1, platform, 1.5, -1, -1, 0);
				MoveToFrame(platform,0,2);
				WaitForStop(platform);
				SetTimerEx(1.0, 1, 0, 0);
			}
			return;
		}

		if (console != -1 && console == consoleref)
		{
			if (canfreezenow)
			{
				// Send a TRIGGER_CARBOFREEZE with its parameters
				SendTrigger(-1, 10001, 0, 0, 0, 0);
				canfreezenow = 0;
				SetTimerEx(freezefreq, 1, 0, 0);
			}
			return;
		}

		return;
	}

	return;

end
