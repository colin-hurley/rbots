# Jedi Knight Cog Script
#
# MDM07_DOORSWITCH.COG
#
# Doors handled by switch with increased max sound distance to 40 meters
#
# [CYW & YB] 
# [JK]
#
# (C) 1997 LucasArts Entertainment Co. All Rights Reserved


symbols

thing       door0                            linkid=1 mask=0x405
thing       door1                            linkid=1 mask=0x405
thing       door2                            linkid=1 mask=0x405
thing       door3                            linkid=1 mask=0x405

surface     switch0                          linkid=2
surface     switch1                          linkid=2

flex        movespeed=8.0
flex        sleeptime=2.5
flex        lightvalue=0.5

sound       onSound=set_hi2.wav              local
sound       offSound=lgclick1.wav            local
sound       locksound=df_door2-3.wav         local

int         doorsector                       local
int         numdoors=0                       local
int         doorstatus=0                     local
int         movestatus=0                     local
int         garbage=-1                       local
int         dummy=0                          local

thing       switchnode0=-1                   nolink   //activate switch0 upon reaching this node
thing       switchnode1=-1                   nolink   //activate switch1 upon reaching this node

int         switchrundir0=-1                          //activate switch0 while running in this direction
int         switchrundir1=-1                          //activate switch1 while running in this direction

# Transient working variables
thing       node                             local
int         rundir                           local
int         i                                local

message     startup
message     activated
message     arrived
message     blocked
message     trigger

end

# ========================================================================================

code
startup:
   if(door0 >= 0) numdoors = numdoors + 1;
   if(door1 >= 0) numdoors = numdoors + 1;
   if(door2 >= 0) numdoors = numdoors + 1;
   if(door3 >= 0) numdoors = numdoors + 1;

   doorsector = GetThingSector(door0);
   SectorAdjoins(doorsector, 0);
   SectorLight(doorsector, lightvalue, 0.0); // add some light to door sector
   return;

# ........................................................................................

activated:
   if (GetSenderId() == 1)
      {
      dummy=PlaySoundThing(locksound, door0, 1.0, -1, -1, 0);
      }
   else if (GetSenderId() == 2)
      {
        call checkstatus;
        if (movestatus) return;
        if (doorstatus == 0)                      // all pieces are at frame 0
           {
            garbage = PlaySoundThing(onSound, door0, 0.6, -1, 40, 0);      // play activate sound
            setwallcel(switch0, 1);
            if (switch1 >= 0) setwallcel(switch1, 1);
            sectoradjoins(doorsector, 1);
            call open_doors;
           }
      }
    return;

# ........................................................................................

arrived:
   call checkstatus;
   if(movestatus) return;
   if(doorstatus == numdoors)
   {                                         // all pieces are at frame 1
      sleep(sleeptime);
      call close_doors;
   }
   else if(doorstatus == 0)
   {                                         // all pieces are at frame 0
      garbage = PlaySoundThing(offSound, door0, 0.6, -1, 40, 0);   // play deactivate sound
      setwallcel(switch0, 0);
      if(switch1 >= 0) setwallcel(switch1, 0);
      sectoradjoins(doorsector, 0);
   }
   return;

# ........................................................................................

blocked:
   call open_doors;
   return;

# ........................................................................................

open_doors:
   movetoframe(door0, 1, movespeed);
   if (door1 >= 0) movetoframe(door1, 1, movespeed);
   if (door2 >= 0) movetoframe(door2, 1, movespeed);
   if (door3 >= 0) movetoframe(door3, 1, movespeed);
   return;

close_doors:
   movetoframe(door0, 0, movespeed);
   if (door1 >= 0) movetoframe(door1, 0, movespeed);
   if (door2 >= 0) movetoframe(door2, 0, movespeed);
   if (door3 >= 0) movetoframe(door3, 0, movespeed);
   return;

checkstatus:
   movestatus = ismoving(door0);
   if (door1 >= 0) movestatus = movestatus + ismoving(door1);
   if (door2 >= 0) movestatus = movestatus + ismoving(door2);
   if (door3 >= 0) movestatus = movestatus + ismoving(door3);

   doorstatus = getcurframe(door0);
   if (door1 >= 0) doorstatus = doorstatus + getcurframe(door1);
   if (door2 >= 0) doorstatus = doorstatus + getcurframe(door2);
   if (door3 >= 0) doorstatus = doorstatus + getcurframe(door3);
   return;

# ........................................................................................

trigger:
	if (GetSourceRef() == 11010) // Bot arrived at node
	{
		#botid = GetParam(0);
		#rbot = GetParam(1);
		node = GetParam(2);
		rundir = GetParam(3);

		for (i = 0 ; i < 2 ; i = i + 1)
		{
			if (node == switchnode0[i]) // bot has reached the switch
			{
				if (switchrundir0[i] == -1 || switchrundir0[i] == rundir) // bot is approaching the door
				{
					// If the doors are closed, activate the switch
					call checkstatus;
					if (movestatus) return;
					if (doorstatus == 0)                      // all pieces are at frame 0
					{
						PlaySoundThing(onSound, door0, 0.6, -1, -1, 0);      // play activate sound
						setwallcel(switch0, 1);
						if (switch1 >= 0) setwallcel(switch1, 1);
						sectoradjoins(doorsector, 1);
						call open_doors;
					}
				}
			}
		}

		return;
	}

	return;

end

