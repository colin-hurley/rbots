# Rbot Backpack Cog (MotS variant)
#
# Type ID: 1050
#
# Rbots originally created by Raynar
# Additional code by Hell Raiser

symbols

message		touched
message		taken
message		removed
message		user7

sound		pickupsnd=BactaPu1.wav				local

# Backpacks
thing		backpack0=-1						local
thing		backpack1=-1						local
thing		backpack2=-1						local
thing		backpack3=-1						local
thing		backpack4=-1						local
thing		backpack5=-1						local
thing		backpack6=-1						local
thing		backpack7=-1						local
thing		backpack8=-1						local
thing		backpack9=-1						local

# Backpack contents
int			bryar0								local
int			bryar1								local
int			bryar2								local
int			bryar3								local
int			bryar4								local
int			bryar5								local
int			bryar6								local
int			bryar7								local
int			bryar8								local
int			bryar9								local

int			strifle0							local
int			strifle1							local
int			strifle2							local
int			strifle3							local
int			strifle4							local
int			strifle5							local
int			strifle6							local
int			strifle7							local
int			strifle8							local
int			strifle9							local

int			thermdets0							local
int			thermdets1							local
int			thermdets2							local
int			thermdets3							local
int			thermdets4							local
int			thermdets5							local
int			thermdets6							local
int			thermdets7							local
int			thermdets8							local
int			thermdets9							local

int			bowcaster0							local
int			bowcaster1							local
int			bowcaster2							local
int			bowcaster3							local
int			bowcaster4							local
int			bowcaster5							local
int			bowcaster6							local
int			bowcaster7							local
int			bowcaster8							local
int			bowcaster9							local

int			repeater0							local
int			repeater1							local
int			repeater2							local
int			repeater3							local
int			repeater4							local
int			repeater5							local
int			repeater6							local
int			repeater7							local
int			repeater8							local
int			repeater9							local

int			raildet0							local
int			raildet1							local
int			raildet2							local
int			raildet3							local
int			raildet4							local
int			raildet5							local
int			raildet6							local
int			raildet7							local
int			raildet8							local
int			raildet9							local

int			seqcharges0							local
int			seqcharges1							local
int			seqcharges2							local
int			seqcharges3							local
int			seqcharges4							local
int			seqcharges5							local
int			seqcharges6							local
int			seqcharges7							local
int			seqcharges8							local
int			seqcharges9							local

int			concrifle0							local
int			concrifle1							local
int			concrifle2							local
int			concrifle3							local
int			concrifle4							local
int			concrifle5							local
int			concrifle6							local
int			concrifle7							local
int			concrifle8							local
int			concrifle9							local

int			blastech0							local
int			blastech1							local
int			blastech2							local
int			blastech3							local
int			blastech4							local
int			blastech5							local
int			blastech6							local
int			blastech7							local
int			blastech8							local
int			blastech9							local

int			stscope0							local
int			stscope1							local
int			stscope2							local
int			stscope3							local
int			stscope4							local
int			stscope5							local
int			stscope6							local
int			stscope7							local
int			stscope8							local
int			stscope9							local

int			flashbombs0							local
int			flashbombs1							local
int			flashbombs2							local
int			flashbombs3							local
int			flashbombs4							local
int			flashbombs5							local
int			flashbombs6							local
int			flashbombs7							local
int			flashbombs8							local
int			flashbombs9							local

int			railseeker0							local
int			railseeker1							local
int			railseeker2							local
int			railseeker3							local
int			railseeker4							local
int			railseeker5							local
int			railseeker6							local
int			railseeker7							local
int			railseeker8							local
int			railseeker9							local

int			carbogun0							local
int			carbogun1							local
int			carbogun2							local
int			carbogun3							local
int			carbogun4							local
int			carbogun5							local
int			carbogun6							local
int			carbogun7							local
int			carbogun8							local
int			carbogun9							local

int			energy0								local
int			energy1								local
int			energy2								local
int			energy3								local
int			energy4								local
int			energy5								local
int			energy6								local
int			energy7								local
int			energy8								local
int			energy9								local

int			power0								local
int			power1								local
int			power2								local
int			power3								local
int			power4								local
int			power5								local
int			power6								local
int			power7								local
int			power8								local
int			power9								local

int			railcharges0						local
int			railcharges1						local
int			railcharges2						local
int			railcharges3						local
int			railcharges4						local
int			railcharges5						local
int			railcharges6						local
int			railcharges7						local
int			railcharges8						local
int			railcharges9						local

int			seekercharges0						local
int			seekercharges1						local
int			seekercharges2						local
int			seekercharges3						local
int			seekercharges4						local
int			seekercharges5						local
int			seekercharges6						local
int			seekercharges7						local
int			seekercharges8						local
int			seekercharges9						local

flex		carbopellets0						local
flex		carbopellets1						local
flex		carbopellets2						local
flex		carbopellets3						local
flex		carbopellets4						local
flex		carbopellets5						local
flex		carbopellets6						local
flex		carbopellets7						local
flex		carbopellets8						local
flex		carbopellets9						local

# Transient working vars
int			i									local
thing		player								local
thing		backpack							local
int			autopickup=0						local
int			autoselect_weapon=-1				local

end

code

touched:
	backpack = GetSenderRef();
	player = GetSourceRef();
	if (GetThingHealth(player) > 0) TakeItem(backpack, player);
	Return;

taken:
	backpack = GetSenderRef();
	player = GetSourceRef();

	jkPrintUNIString(player, 200);

	// Do effects.
	PlaySoundLocal(pickupsnd, 1, 0, 0);
	AddDynamicTint(player, 0.0, 0.0, 0.2);

	// Pickup the backpack
	call pickup_backpack;

	// New ammo is already assigned, so this is like a autopickup/autoreload mix,
	// it will select the best weapon overall with all the stuff in the backpack
	// taken into account.

	autopickup = GetAutoPickup();
	autoselect_weapon = AutoSelectWeapon(player, 0);

	if(autopickup & 1)
	{
		if( !((autopickup & 2) && ((autoselect_weapon == 4) || (autoselect_weapon == 8) || (autoselect_weapon == 9)) ))
		{
			if(!((autopickup & 4) && (GetWeaponPriority(player, GetWeaponBin(GetCurWeapon(player)), 0) >= GetWeaponPriority(player, autoselect_weapon, 0) )))
			{
				if(!((autopickup & 8) && (GetWeaponBin(GetCurWeapon(player)) == jkGetMultiParam(1))))
				{
					SelectWeapon(player, autoselect_weapon);
					Return;
				}
			}
		}
	}

	Return;

removed:
	// Clear stored backpack data, backpack was picked up or timed out and is being removed
	backpack = GetSenderRef();
	player = -1; // no one is picking up this backpack

	// Clear backpack data slot
	call pickup_backpack;

	return;

// Execute public API function for this cog
// Param 0 = Function ID
// The remaining params will be processed according to the requested function
// For example, param1 might be an index into an array.
// Returns the function-specific result of the execution
user7:
	if (GetParam(0) == 10502010) // fill backpack
	{
		backpack = GetParam(1);
		player = GetParam(2);

		call fill_backpack;

		ReturnEx(0);
		return;
	}

	if (GetParam(0) == 10502020) // pickup backpack
	{
		backpack = GetParam(1);
		player = GetParam(2);

		call pickup_backpack;

		TakeItem(backpack, player);
		ReturnEx(0);
		return;
	}

	// Report error if function ID does not match anything above
	jkStringClear();
	jkStringConcatFormattedInt(GetSenderRef(), "rbot-backpack.cog :: ERROR :: Cog %d");
	jkStringConcatFormattedInt(GetParam(0), " has requested an invalid function ID, %d");
	jkStringOutput(-1, -1);
	return;

# Params: backpack, player
fill_backpack:
	for (i = 0 ; i < 10 ; i = i + 1)
	{
		if (backpack0[i] == -1 || backpack0[i] == backpack) // found free slot to store the backpack data
		{
			backpack0[i] = backpack; // reserve data slot for this backpack

			if (player != -1)
			{
				if (GetThingType(player) == 2) // bot
				{
					bryar0[i]         = SendMessageEx(GetThingClassCog(player), user7, 10231006, 2, -1, -1);
					strifle0[i]       = SendMessageEx(GetThingClassCog(player), user7, 10231006, 3, -1, -1);
					thermdets0[i]     = SendMessageEx(GetThingClassCog(player), user7, 10231006, 4, -1, -1);
					bowcaster0[i]     = SendMessageEx(GetThingClassCog(player), user7, 10231006, 5, -1, -1);
					repeater0[i]      = SendMessageEx(GetThingClassCog(player), user7, 10231006, 6, -1, -1);
					raildet0[i]       = SendMessageEx(GetThingClassCog(player), user7, 10231006, 7, -1, -1);
					seqcharges0[i]    = SendMessageEx(GetThingClassCog(player), user7, 10231006, 8, -1, -1);
					concrifle0[i]     = SendMessageEx(GetThingClassCog(player), user7, 10231006, 9, -1, -1);
					blastech0[i]      = SendMessageEx(GetThingClassCog(player), user7, 10231006, 12, -1, -1);
					stscope0[i]       = SendMessageEx(GetThingClassCog(player), user7, 10231006, 13, -1, -1);
					flashbombs0[i]    = SendMessageEx(GetThingClassCog(player), user7, 10231006, 14, -1, -1);
					railseeker0[i]    = SendMessageEx(GetThingClassCog(player), user7, 10231006, 17, -1, -1);
					carbogun0[i]      = SendMessageEx(GetThingClassCog(player), user7, 10231006, 20, -1, -1);
					energy0[i]        = SendMessageEx(GetThingClassCog(player), user7, 10231002, -1, -1, -1);
					power0[i]         = SendMessageEx(GetThingClassCog(player), user7, 10231003, -1, -1, -1);
					railcharges0[i]   = SendMessageEx(GetThingClassCog(player), user7, 10231005, -1, -1, -1);
					seekercharges0[i] = SendMessageEx(GetThingClassCog(player), user7, 10231008, -1, -1, -1);
					carbopellets0[i]  = SendMessageEx(GetThingClassCog(player), user7, 10231009, -1, -1, -1);
				}

				if (GetThingType(player) == 10) // player
				{
					bryar0[i]         = GetInv(player, 122);
					strifle0[i]       = GetInv(player, 123);
					thermdets0[i]     = GetInv(player, 124);
					bowcaster0[i]     = GetInv(player, 135);
					repeater0[i]      = GetInv(player, 126);
					raildet0[i]       = GetInv(player, 127);
					seqcharges0[i]    = GetInv(player, 128);
					concrifle0[i]     = GetInv(player, 129);
					blastech0[i]      = GetInv(player, 132);
					stscope0[i]       = GetInv(player, 133);
					flashbombs0[i]    = GetInv(player, 134);
					railseeker0[i]    = GetInv(player, 137);
					carbogun0[i]      = GetInv(player, 140);
					energy0[i]        = GetInv(player, 11);
					power0[i]         = GetInv(player, 12);
					railcharges0[i]   = GetInv(player, 15);
					seekercharges0[i] = GetInv(player, 91);
					carbopellets0[i]  = GetInv(player, 90);
				}
			}

			return;
		}
	}
	return;

# Params: backpack, player
pickup_backpack:
	for (i = 0 ; i < 10 ; i = i + 1)
	{
		if (backpack0[i] == backpack) // found the slot containing the backpack data
		{
			backpack0[i] = -1; // clear data slot so another backpack can use it

			if (player != -1)
			{
				if (GetThingType(player) == 2) // bot
				{
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 2, bryar0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 3, strifle0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 4, thermdets0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 5, bowcaster0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 6, repeater0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 7, raildet0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 8, seqcharges0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 9, concrifle0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 12, blastech0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 13, stscope0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 14, flashbombs0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 17, railseeker0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232506, 20, carbogun0[i], -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232502, energy0[i], -1, -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232503, power0[i], -1, -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232505, railcharges0[i], -1, -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232508, seekercharges0[i], -1, -1);
					SendMessageEx(GetThingClassCog(player), user7, 10232509, carbopellets0[i], -1, -1);
				}

				if (GetThingType(player) == 10) // player
				{
					ChangeInv(player, 122, bryar0[i]);
					ChangeInv(player, 123, strifle0[i]);
					ChangeInv(player, 124, thermdets0[i]);
					ChangeInv(player, 135, bowcaster0[i]);
					ChangeInv(player, 126, repeater0[i]);
					ChangeInv(player, 127, raildet0[i]);
					ChangeInv(player, 128, seqcharges0[i]);
					ChangeInv(player, 129, concrifle0[i]);
					ChangeInv(player, 132, blastech0[i]);
					ChangeInv(player, 133, stscope0[i]);
					ChangeInv(player, 134, flashbombs0[i]);
					ChangeInv(player, 137, railseeker0[i]);
					if (seqcharges0[i] > 0) ChangeInv(player, 138, 1); // sequencer charge manual mode
					ChangeInv(player, 140, carbogun0[i]);
					ChangeInv(player, 11, energy0[i]);
					ChangeInv(player, 12, power0[i]);
					ChangeInv(player, 15, railcharges0[i]);
					ChangeInv(player, 91, seekercharges0[i]);
					ChangeInv(player, 90, carbopellets0[i]);
				}
			}

			return;
		}
	}
	return;

end
