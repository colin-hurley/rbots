# Rbot Item Special Cog
#
# Server-only
# Type ID: 1041
#
# This cog supplies special information for items that bots can pick up.
#
# To allow unlimited expansion and avoid the need to maintain multiple cog
# reference symbols, multiple cogs of this type will self-organize into a
# linked list at startup.
#
# Rbots originally created by Raynar

flags=0x280

symbols

message		startup
message		trigger
message		user7

# Special items
thing		item0=-1							nolink
thing		item1=-1							nolink
thing		item2=-1							nolink
thing		item3=-1							nolink
thing		item4=-1							nolink
thing		item5=-1							nolink
thing		item6=-1							nolink
thing		item7=-1							nolink
thing		item8=-1							nolink
thing		item9=-1							nolink

# Flags for each special item
int			spinf0=0
int			spinf1=0
int			spinf2=0
int			spinf3=0
int			spinf4=0
int			spinf5=0
int			spinf6=0
int			spinf7=0
int			spinf8=0
int			spinf9=0

# The first cog in the linked list
cog			spitem=-1							local
# The next cog following this one in the linked list
cog			nextspitem=-1						local

# Working variables
int			i									local
thing		item								local

end

# ========================================================================================

code

startup:
	if (IsMulti() && !IsServer()) return; // Host only

	// Allow other server-side cogs to discover this one by its type
	// Only send the discovery trigger for the head of the list
	// Any other cogs can append themselves to the list afterward
	if (spitem == -1) SendTrigger(-1, 1852000, 1041, GetSelfCog(), -1, -1); // register as head of list
	else SendMessageEx(spitem, user7, 10410001, GetSelfCog(), -1, -1); // append to list
	return;

trigger:
	if (IsMulti() && !IsServer()) return; // Host only

	if (GetSourceRef() == 1852000) // Rbot cog type discovery
	{
		if (GetParam(0) == 1041) // rbot-item-special.cog
		{
			spitem = GetParam(1);
		}
		return;
	}

	return;

user7:
	if (GetParam(0) == 10410001) // Append cog to list
	{
		if (nextspitem == -1) nextspitem = GetParam(1); // this is the end of the list, append here
		else SendMessageEx(nextspitem, user7, GetParam(0), GetParam(1), -1, -1); // try to append to next cog
		return;
	}

	if (GetParam(0) == 10413000) // Get item special mode
	{
		item = GetParam(1); // the item

		// Search current cog for special info
		for (i = 0 ; i < 10 ; i = i + 1)
		{
			if (item0[i] == item && spinf0[i]) // This is the requested item and it has special info
			{
				ReturnEx(spinf0[i]);
				return;
			}
		}

		// No special info specified in this cog, see if we can delegate to the next cog in the list
		if (nextspitem != -1)
		{
			// Recursively delegate to next cog in the list
			ReturnEx(SendMessageEx(nextspitem, user7, GetParam(0), GetParam(1), GetParam(2), GetParam(3)));
			return;
		}

		// No more cogs in the list, no special info
		ReturnEx(0x0);
		return;
	}

	// Report error if function ID does not match anything above
	jkStringClear();
	jkStringConcatFormattedInt(GetSenderRef(), "rbot-item-special.cog :: ERROR :: Cog %d");
	jkStringConcatFormattedInt(GetParam(0), " has requested an invalid function ID, %d");
	jkStringOutput(-1, -1);
	return;

end
