# Rbot Skin Cog (MotS variant)
#
# Server-only
# Type ID: 1030
#
# Rbots originally created by Raynar
# Additional code by Hell Raiser

flags=0x80

symbols

message		startup
message		user7

int			skinid=0
int			sabercolor=0

# The template to create the bot
template	rtemp=rbot_kylered

# Weapon mesh models
model		weapmesh0=sabg.3do					local
model		weapmesh1=fistg.3do					local
model		weapmesh2=bryg.3do					local
model		weapmesh3=strg.3do					local
model		weapmesh4=detg.3do					local
model		weapmesh5=bowg.3do					local
model		weapmesh6=rptg.3do					local
model		weapmesh7=rldg.3do					local
model		weapmesh8=seqg.3do					local
model		weapmesh9=cong.3do					local
model		weapmesh10=sabg.3do					local
model		weapmesh11=fistg.3do				local
model		weapmesh12=blsg.3do					local
model		weapmesh13=sscg.3do					local
model		weapmesh14=flsg.3do					local
model		weapmesh15=fistg.3do				local
model		weapmesh16=fistg.3do				local
model		weapmesh17=skrg.3do					local
model		weapmesh18=fistg.3do				local
model		weapmesh19=fistg.3do				local
model		weapmesh20=cbng.3do					local

# Switched on lightsaber weapon mesh models
model		sabermesh0=sabgred.3do				local
model		sabermesh1=sabgblue.3do				local
model		sabermesh2=sabggreen.3do			local
model		sabermesh3=sabgorange.3do			local
model		sabermesh4=sabgpurple.3do			local
model		sabermesh5=sabgyellow.3do			local

# Transient working vars
thing		rbot								local
thing		spawnpoint							local
int			curweap								local

end

code

startup:
	if (IsMulti() && !IsServer()) return; // Host only

	// Allow other server-side cogs to discover this one by its type
	SendTrigger(-1, 1852000, 1030, GetSelfCog(), skinid, -1);

	// Initialize lightsaber weapon mesh to specified color
	if (0 <= sabercolor && sabercolor <= 5)
	{
		weapmesh10 = sabermesh0[sabercolor];
	}

	return;

// Execute public API function for this cog
// Param 0 = Function ID
// The remaining params will be processed according to the requested function
// For example, param1 might be an index into an array.
// Returns the function-specific result of the execution
user7:
	if (GetParam(0) == 10303000) // create bot
	{
		spawnpoint = GetParam(1);
		rbot = CreateThing(rtemp, spawnpoint);
		ReturnEx(rbot);
		return;
	}

	if (GetParam(0) == 10303010) // change weapon model
	{
		rbot = GetParam(1);
		curweap = GetParam(2);
		jkSetWeaponMesh(rbot, weapmesh0[curweap]);
		ReturnEx(0);
		return;
	}

	// Report error if function ID does not match anything above
	jkStringClear();
	jkStringConcatFormattedInt(GetSenderRef(), "rbot-skin.cog :: ERROR :: Cog %d");
	jkStringConcatFormattedInt(GetParam(0), " has requested an invalid function ID, %d");
	jkStringOutput(-1, -1);
	return;

end
